include "stdio.h"
include "stdlib.h"
include "string.h"
include "sys/event.h"
include "three/c11/threads.h"
include "three/runtime/closure.h" // this should not be necessary

namespace Core::Event

  public
  enum Type
    Read
    Write
  end

  struct Monitor
    thrd_t thread
    Int    kq
    *{Natural handle, Natural data} closure_ptr
  end

  private
  def event_thread_entry(*Void arg; Int)
    *Monitor monitor = arg
    kevent64_s ke

    loop
      memset(&ke, 0, sizeof(kevent64_s))

      printf("parking thread on kevent64\n")
      Int result = kevent64(monitor->kq, null, 0, &ke, 1, 0, null)

      printf("woke for %d", result)

      // shenanigans
      if monitor->closure_ptr
        *{Natural handle, Natural data} closure_a = monitor->closure_ptr
        {Natural handle, Natural data} closure = *closure_a

        closure(ke.ident, ke.data)
      end
    end

    return 0
  end

  public
  def monitor_create(; *Monitor)
    *Monitor monitor

    monitor = malloc(sizeof(Monitor))
    monitor->kq = kqueue()
    monitor->closure_ptr = null

    thrd_create(&monitor->thread, Core::Event::event_thread_entry, monitor)

    return monitor
  end

  def Monitor.set_event_handler({Natural handle, Natural data} closure)
    if self->closure_ptr
      three_closure_release(self->closure_ptr)
    end

    self->closure_ptr = three_closure_copy(closure)
  end

  def Monitor.watch(Type type, Natural handle)
    kevent64_s ke

    memset(&ke, 0, sizeof(kevent64_s))

    ke.ident = handle
    switch type
    case Core::Event::Type::Read
      ke.filter = -1 // EVFILT_READ
      ke.flags = 0x0001 | 0x0004 // EV_ADD | EV_ENABLE
      ke.data = 5
    end

    if kevent64(self->kq, &ke, 1, null, 0, 0, null) == -1
      printf("Unable to arm the event\n")
    end
  end
end
